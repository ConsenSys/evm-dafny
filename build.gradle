/*
 * Copyright 2022 ConsenSys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License. You may obtain
 * a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software dis-
 * tributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
// Apply eclipse plugin to add support for Eclipse
apply plugin: 'eclipse'
// Apply application plugin for generating start scripts
apply plugin: 'application'

// ======================================================================
// Constants (Dafny 3)
// ======================================================================

final DAFNY_BASE_FLAGS = [
    '/compileVerbose:0',
    '/compileTarget:java',
    '/functionSyntax:4',
    '/noExterns',
    '/quantifierSyntax:4',
    '/rlimit:100000',
    '/vcsLoad:2.0'
]

final DAFNY_BUILD_FLAGS = DAFNY_BASE_FLAGS + [
    '/verifyAllModules',
    '/out:build/evm'
]

final DAFNY_TEST_FLAGS = DAFNY_BASE_FLAGS + [
    '/runAllTests:1',
    '/out:build/dafny-tests',
    '/compile:4'
]

// ======================================================================
// Constants (Dafny 4)
// ======================================================================

final DAFNY4_BASE_FLAGS = [
    '--target','java',
    '--cores','4',
    '--function-syntax','4',
    '--quantifier-syntax','4'
]

final DAFNY4_BUILD_FLAGS = [
    'translate',
    '--output','build/evm',
    '--include-runtime',
    '--verify-included-files'
] + DAFNY4_BASE_FLAGS

final DAFNY4_TEST_FLAGS = [
    'test'
] + DAFNY4_BASE_FLAGS

// ======================================================================
// Dafny Build
// ======================================================================

task compileDafny {
    // Specify inputs
    inputs.files(fileTree('src/dafny/') { include '**/*.dfy' } )
    // Specify outputs
    outputs.files(fileTree('build/evm-java') { include '**/*.java' } )
    // Enable caching
    outputs.cacheIf { true }
    // Specify actions
    doLast {
        // Generate Dafny Source
        exec {
            executable 'dafny'
            args DAFNY_BUILD_FLAGS + ['src/dafny/evm.dfy','src/dafny/evms/berlin.dfy']
        }
        // Delete general file containing extern definitions.
        delete "build/evm-java/External_Compile"
    }
}

task cleanDafny {
    doLast {
        delete "build/evm-java"
    }
}

clean.dependsOn cleanDafny

// ======================================================================
// Dafny Tests
// ======================================================================

task testDafny {
    // Specify inputs
    inputs.files(fileTree('src') { include '**/*.dfy' } )
    // Specify outputs
    outputs.files(fileTree('build/dafny-tests-java') { include '**/*.java' } )
    // Require code gen
    dependsOn compileDafny
    // Specify actions
    doLast {
        // Verify and execute all Dafny tests found in the test
        // directory.
        fileTree("src/test/dafny").include('**/*.dfy').each {
            File file -> {
                exec {
                    executable 'dafny'
                    args DAFNY_TEST_FLAGS + [file]
                }
            }
        }
    }
}

// ======================================================================
// Java Build
// ======================================================================

repositories {
    // Use MavenCentral for resolving dependencies
    mavenCentral()
}

test {
    // Specify inputs
    inputs.files(fileTree('tests') { include '**/*.json' } )
    // Ensure can see stdout
    testLogging.showStandardStreams = true
    // Ensure enough memory
    jvmArgs '-Xmx2G'
    //
    useJUnitPlatform()
    filter {
        includeTestsMatching('dafnyevm.Tests')
        includeTestsMatching('dafnyevm.GeneralStateTests')
    }
}

// Specify that should run compileDafny before compileJava.
// Otherwise, the Java compiler cannot find up-to-date Dafny tests!
compileJava.dependsOn testDafny

sourceSets {
    main {
        java {
            // Add Dafny output directory to Java build path.
            srcDirs("build/evm-java")
        }
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation("commons-cli:commons-cli:1.5.0")
    implementation("org.apache.commons:commons-lang3:3.12.0")
    implementation("org.json:org.json:chargebee-1.0")
    implementation("org.web3j:utils:5.0.0")
    implementation("org.web3j:rlp:5.0.0")
    implementation("org.web3j:crypto:5.0.0")
    implementation("org.whiley:evmtools:0.3.25")
    implementation files('build/evm-java/DafnyRuntime.jar')
    //
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

// ======================================================================
// Trace Generation
// ======================================================================

// A default task for convert reference tests into the trace format
// required for testing the Dafny evm.
task testgen(type: JavaExec) {
    // Specify inputs
    inputs.files("tests/includes.txt", "tests/excludes.txt",
        fileTree('fixtures/') { include '**/*.json' } )
    // Specify outputs
    outputs.files(fileTree('tests') { include '**/*.json' } )
    // Enable caching
    outputs.cacheIf { true }
    // Specify actions
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'evmtools.Main'
    // arguments to pass to the application
    args '-incremental'
    args '-fork'
    args 'Berlin'
    args '-dir'
    args 'fixtures'
    args '-out'
    args 'tests'
    args '-includes'
    args 'tests/includes.txt'
    args '-excludes'
    args 'tests/excludes.txt'
}

// ======================================================================
// Java Application
// ======================================================================

application {
    mainClass = 'dafnyevm.Main'
    applicationName = 'dafnyevm'
}
